buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:3.1.1"
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'com.github.johnrengelman.shadow'

apply from: "$rootDir/gradle/additional-artifacts.gradle"
apply from: "$rootDir/gradle/dependencies.gradle"
apply from: "$rootDir/gradle/publishing.gradle"
apply from: "$rootDir/gradle/documentation.gradle"
apply from: "$rootDir/gradle/release.gradle"

repositories {
    jcenter()
}

dependencies {
    ext.jcloudsVersion = '2.1.2'
    ext.autoValueVersion = '1.6.2'
    ext.guiceVersion = '4.2.2'

    compile ("com.google.inject:guice:${guiceVersion}")
    compile ("com.google.inject.extensions:guice-assistedinject:${guiceVersion}")

    compile ("org.apache.jclouds:jclouds-core:${jcloudsVersion}")
    compile ('com.google.auto.service:auto-service:1.0-rc6')
    compile ("com.google.auto.value:auto-value-annotations:${autoValueVersion}")

    compileOnly ("com.google.auto.value:auto-value:${autoValueVersion}")

    testCompile ("org.apache.jclouds:jclouds-core:${jcloudsVersion}:tests")
    testCompile ("org.apache.jclouds.driver:jclouds-slf4j:${jcloudsVersion}")
    testCompile ('org.testng:testng:6.13.1')
    testCompile ('org.assertj:assertj-core:3.9.0')
    testCompile ('com.squareup.okhttp:mockwebserver:2.7.5')
    testCompile ('ch.qos.logback:logback-core:1.2.3')
    testCompile ('ch.qos.logback:logback-classic:1.2.3')
}

ext.compatibilityVersion = '1.8'
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

jar {
    manifest {
        attributes 'Implementation-Title': 'Artifactory REST client',
                   'Implementation-Version': version,
                   'Built-By': System.getProperty('user.name'),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty('java.version'),
                   'Built-Gradle': gradle.gradleVersion
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ["-Xlint:-options"]
}

task mockTest(type: Test) {
    useTestNG()
    include "**/**MockTest.class"
    testLogging {
        showStandardStreams = true
        events 'started', 'passed', 'failed'
    }
}

task integTest(type: Test, dependsOn: ['mockTest']) {
    useTestNG()
    include "**/StorageApiLiveTest.class"
    testLogging {
        showStandardStreams = true
        events 'started', 'passed', 'failed'
    }

    def authentication = [:]
    def possibleAuth = project.findProperty('testArtifactoryRestCredentials')
    if (possibleAuth) {
        authentication['test.artifactory.rest.credentials'] = possibleAuth
    } else {
        possibleAuth = project.findProperty('testArtifactoryRestToken')
        if (possibleAuth) {
            authentication['test.artifactory.rest.token'] = possibleAuth
        } else {
            logger.quiet 'No authentication parameters found. Assuming anonymous...'
        }
    }

    // property 'test.artifactory.endpoint' needs to be
    // hard-coded in for jclouds test framework
    systemProperties = ["test.artifactory.endpoint" : "${testArtifactoryRestEndpoint}",
                        "test.artifactory.docker.repo" : "${testArtifactoryRestDockerRepo}",
                        "test.artifactory.docker.repo.promoted" : "${testArtifactoryRestDockerPromotedRepo}",
                        "test.artifactory.docker.image" : "${testArtifactoryRestDockerImage}",
                        "test.artifactory.docker.tag" : "${testArtifactoryRestDockerTag}"] << authentication
}

